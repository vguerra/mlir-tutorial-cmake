#ifndef LIB_DIALECT_POLY_POLYOPS_TD_
#define LIB_DIALECT_POLY_POLYOPS_TD_

include "PolyTypes.td"
include "mlir/Interfaces/InferTypeOpInterface.td"
include "mlir/Interfaces/SideEffectInterfaces.td"

// Type constraint for poly binop arguments: polys, vector or tensor of polys.
def PolyOrContainer : TypeOrContainer<Polynomial, "poly-or-container">;

class Poly_BinOp<string mnemonic> : Op<Poly_Dialect, mnemonic, [Pure, ElementwiseMappable, SameOperandsAndResultType]> {
    let arguments = (ins PolyOrContainer:$lhs, PolyOrContainer:$rhs);
    let results = (outs PolyOrContainer:$output);
    let assemblyFormat = "$lhs `,` $rhs attr-dict `:` qualified(type($output))";
    let hasFolder = 1;
}

def Poly_AddOp : Poly_BinOp<"add"> {
    let summary = "Addition operation between polynomials";
}

def Poly_SubOp : Poly_BinOp<"sub"> {
    let summary = "Subtraction operation between polynomials.";
}

def Poly_MulOp : Poly_BinOp<"mul"> {
    let summary = "Multiplication operation between polynomials.";
}

def Poly_FromTensorOp : Op<Poly_Dialect, "from_tensor", [Pure]> {
    let summary = "Creates a Polynomial from integer coefficients stored in a tensor.";
    let arguments = (ins TensorOf<[AnyInteger]>:$input);
    let results = (outs Polynomial:$output);
    let assemblyFormat = "$input attr-dict `:` type($input) `->` qualified(type($output))";
    let hasFolder = 1;
}

def Poly_EvalOp : Op<Poly_Dialect, "eval"> {
    let summary = "Evaluates a Polynomial at a given input value.";
    let arguments = (ins Polynomial:$input, AnyInteger:$point);
    let results = (outs AnyInteger:$output);
    let assemblyFormat = "$input `,` $point attr-dict `:` `(` qualified(type($input)) `,` type($point) `)` `->` type($output)";
}

def Poly_ConstantOp : Op<Poly_Dialect, "constant", [Pure, ConstantLike]> {
    let summary = "Define a constant polynomial via an attribute";
    let arguments = (ins AnyIntElementsAttr:$coefficients);
    let results = (outs Polynomial:$output);
    let assemblyFormat = "$coefficients attr-dict `:` qualified(type($output))";
    let hasFolder = 1;
}

#endif  // LIB_DIALECT_POLY_POLYOPS_TD_